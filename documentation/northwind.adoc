= Northwind Graph
:neo4j-version: 2.3.0

:toc:

== From RDBMS to Graph, using a classic dataset

The__Northwind Graph__ demonstrates how to migrate from a relational
database to Neo4j. The transformation is iterative and deliberate,
emphasizing the conceptual shift from relational tables to the nodes and
relationships of a graph.

This guide will show you how to:

1.  Load: create data from external CSV files
2.  Index: index nodes based on label
3.  Relate: transform foreign key references into data relationships
4.  Promote: transform join records into relationships


== Product Catalog

Northwind sells food products in a few categories, provided by
suppliers. Let's start by loading the product catalog tables.

The load statements to the right require public internet
access.`LOAD CSV` will retrieve a CSV file from a valid URL, applying a
Cypher statement to each row using a named map (here we're using the
name `row`).

image:https://dev.assets.neo4j.com.s3.amazonaws.com/wp-content/uploads/20160211151109/product-category-supplier.png[image]

== Load records

.Load products
[source,cypher]
----
LOAD CSV WITH HEADERS FROM "https://data.neo4j.com/northwind/products.csv" AS row
CREATE (n:Product)
SET n = row,
  n.unitPrice = toFloat(row.unitPrice),
  n.unitsInStock = toInt(row.unitsInStock), n.unitsOnOrder = toInt(row.unitsOnOrder),
  n.reorderLevel = toInt(row.reorderLevel), n.discontinued = (row.discontinued <> "0")
----

.Load categories
[source,cypher]
----
LOAD CSV WITH HEADERS FROM "https://data.neo4j.com/northwind/categories.csv" AS row
CREATE (n:Category)
SET n = row
----

.Load suppliers
[source,cypher]
----
LOAD CSV WITH HEADERS FROM "https://data.neo4j.com/northwind/suppliers.csv" AS row
CREATE (n:Supplier)
SET n = row
----

== Create indexes

.Create indexes for the loaded nodes
[source,cypher]
----
CREATE INDEX ON :Product(productID);
CREATE INDEX ON :Category(categoryID);
CREATE INDEX ON :Supplier(supplierID);
----

== Product Catalog Graph

The products, categories and suppliers are related through foreign key
references. Let's promote those to data relationships to realize the
graph.

image:https://assets.neo4j.com/wp-content/uploads/20160211151108/product-graph.png[image]

=== Create data relationships

.Connect products to categories
[source,cypher]
----
MATCH (p:Product),(c:Category)
WHERE p.categoryID = c.categoryID
CREATE (p)-[:PART_OF]->(c)
----

Note you only need to compare property values like this when first
creating relationships

Calculate join, materialize relationship.
(See https://neo4j.com/developer/guide-importing-data-and-etl[importing guide] for more details)

.Connect products to suppliers
[source,cypher]
----
MATCH (p:Product),(s:Supplier)
WHERE p.supplierID = s.supplierID
CREATE (s)-[:SUPPLIES]->(p)
----

Note you only need to compare property values like this when first
creating relationships

== Querying Product Catalog Graph

Lets try some queries using patterns.

image:https://assets.neo4j.com/wp-content/uploads/20160211151108/product-graph.png[image]

=== Query using patterns



.List the product categories provided by each supplier
[source,cypher]
----
MATCH (s:Supplier)-->(:Product)-->(c:Category)
RETURN s.companyName as Company, collect(distinct c.categoryName) as Categories
----
//table

.List suppliers for the "Produce" category
[source,cypher]
----
MATCH (c:Category {categoryName:"Produce"})<--(:Product)<--(s:Supplier)
RETURN DISTINCT s.companyName as ProduceSuppliers
----
//table

Find the produce suppliers.

== Customer Orders

Northwind customers place orders which may detail multiple
products. 

image::https://assets.neo4j.com/wp-content/uploads/20160211151108/customer-orders.png[]

=== Load and index records

.Load customers
[source,cypher]
----
LOAD CSV WITH HEADERS FROM "https://data.neo4j.com/northwind/customers.csv" AS row
CREATE (n:Customer)
SET n = row
----

.Load orders
[source,cypher]
----
LOAD CSV WITH HEADERS FROM "https://data.neo4j.com/northwind/orders.csv" AS row
CREATE (n:Order)
SET n = row
----

.Create indexes for loaded nodes
[source,cypher]
----
CREATE INDEX ON :Customer(customerID);
CREATE INDEX ON :Order(orderID);
----

== Create data relationships

.Connect customers to their orders
[source,cypher]
----
MATCH (c:Customer),(o:Order)
WHERE c.customerID = o.customerID
CREATE (c)-[:PURCHASED]->(o)
----

Note you only need to compare property values like this when first
creating relationships

== Customer Order Graph

Notice that Order Details are always part of an Order and that
they__relate__ the Order to a Product â€” they're a join table. Join
tables are always a sign of a data relationship, indicating shared
information between two other records.

Here, we'll directly promote each OrderDetail record into a relationship
in the graph. image:https://assets.neo4j.com/wp-content/uploads/20160211151107/order-graph.png[]


=== Load and index records

.Load order details and use them to connect orders to products
[source,cypher]
----
LOAD CSV WITH HEADERS FROM "https://data.neo4j.com/northwind/order-details.csv" AS row
MATCH (p:Product), (o:Order)
WHERE p.productID = row.productID AND o.orderID = row.orderID
CREATE (o)-[details:ORDERS]->(p)
SET details = row,
  details.quantity = toInt(row.quantity)
----

Note you only need to compare property values like this when first
creating relationships

== Query using patterns

.Find total quantity per customer in the "Produce" category
[source,cypher]
----
MATCH (cust:Customer)-[:PURCHASED]->(:Order)-[o:ORDERS]->(p:Product),
      (p)-[:PART_OF]->(c:Category {categoryName:"Produce"})
RETURN DISTINCT cust.contactName as CustomerName, SUM(o.quantity) AS TotalProductsPurchased
----
//table

_More Resources_

* https://neo4j.com/developer/guide-importing-data-and-etl/[Full
Northwind import example]
* https://neo4j.com/developer[Developer resources]

